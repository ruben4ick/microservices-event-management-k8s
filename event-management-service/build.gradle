dependencies {
	implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'
    implementation 'org.springframework.boot:spring-boot-starter-aop'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation("io.micrometer:micrometer-registry-otlp")
    implementation("io.micrometer:micrometer-tracing-bridge-otel")
    implementation 'io.micrometer:micrometer-registry-prometheus'
    testImplementation 'io.rest-assured:rest-assured:5.4.0'
    testImplementation 'net.datafaker:datafaker:2.4.1'
    testImplementation 'org.springframework.cloud:spring-cloud-starter-contract-stub-runner'
    runtimeOnly("io.opentelemetry:opentelemetry-exporter-otlp")
}

tasks.named('processResources') {
    exclude '**/*.proto'
}

tasks.register('generateAPIFile') {
	doLast {
		def projectRoot = project.projectDir
		def newFile = new File(projectRoot, "open-api.json")
		if (!newFile.exists()) {
			newFile.createNewFile()
		}
		exec {
			commandLine 'curl', '-o', newFile.getAbsolutePath(), 'http://localhost:8081/v3/api-docs'
		}
	}
}

tasks.withType(Test).configureEach {
    def envFile = file("$rootDir/.env")
    if (!envFile.exists()) envFile = file("$projectDir/.env")
    if (envFile.exists()) {
        envFile.readLines()
                .findAll { it && !it.startsWith("#") && it.contains("=") }
                .each {
                    def (k, v) = it.split("=", 2)
                    environment k.trim(), v.trim()
                }
    }
}
